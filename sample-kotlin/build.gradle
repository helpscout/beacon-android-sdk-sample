plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

apply from: '../signing/signing.gradle'
apply from: '../dependencies.gradle'

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "net.helpscout.samples.beacon.kotlin"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            // Beacon SDK supports Proguard, no additional config is needed.
            // Since version 2.3.2 support for proguard-android-optimize.txt
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        // Since SDK 4.0.0 we moved to Android Gradle Plugin 7.x which requires the sourceCompatibility and
        // targetCompatibility to Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        // recommend Kotlin jvm target to be Java 11
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    lint {
        disable 'MissingTranslation'
        ignoreWarnings true
    }
    namespace 'net.helpscout.samples.beacon.kotlin'
}

dependencies {
    implementation "com.google.android.material:material:$materialVersion"

    // Add this to your dependencies to install Beacon SDK to your app
    implementation "com.helpscout:beacon:$beaconVersion"

    testImplementation "junit:junit:$jUnitVersion"
}


